apply plugin: 'com.android.application'

def getOsDefinitions() {
    if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
        return '-DANDROID_ON_WINDOWS=TRUE'
    } else if (org.gradle.internal.os.OperatingSystem.current().isMacOsX()) {
        return '-DANDROID_ON_MACOS=TRUE'
    } else if (org.gradle.internal.os.OperatingSystem.current().isLinux()) {
        return '-DANDROID_ON_LINUX=TRUE'
    }
    return ''
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "%A2D_APP_IDENTIFIER%"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static', getOsDefinitions()
                cppFlags ""
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

task copyAssets(type: Copy) {
    description = 'Copy resources to assets folder'
    from '../../../resources'
    into 'src/main/assets'
}

preBuild.dependsOn copyAssets


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
